//===== Cronus Script ===========================================
//= Guia de Lighthalzen
//===== Por: ====================================================
//= MasterOfMuppets; L0ne_W0lf
//===== Versão Atual: =========================================== 
//= 1.2e
//===== Compatível Com: ======================================
//= Cronus SVN
//===== Changelog: ============================================== 
//= 1.0 First Version [MasterOfMuppets]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.2a Traduzido [Minverva]
//= 1.2b Revisado [Page]
//= 1.2c Re-revisado. [RoM]
//= 1.2d Removido show	[Yum]
//= 1.2e Revisado	[KyukE]
//===============================================================

lighthalzen,207,310,5	script	Guia#lhz::LhzGuide	4_M_EIN_SOLDIER,{
	cutin "ein_soldier",2;
	mes "[Guia de Lighthalzen]";
	mes "Bem-vindo a Lighthalzen,";
	mes "a Corporação Cidade-Estado.";
	mes "Se você precisar de orientação";
	mes "pela cidade, sinta-se à vontade";
	mes "para me perguntar e eu farei meu";
	mes "melhor para ajudá-lo.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Indicações","Limpar Indicações","Tutorial","Cancelar")) {
		case 1:
			mes "[Guia de Lighthalzen]";
			mes "Por favor fique avisado que eu";
			mes "estou no comando de promover";
			mes "informações do Distrito do Leste";
			mes "de Lighthalzen. Agora, por favor";
			mes "selecione o local que";
			mes "deseja saber mais sobre.";
			if (.@compass_check == 0) {
				next;
				mes "[Guia de Lighthalzen]";
				mes "Mas antes disso,";
				mes "você gostaria que eu";
				mes "marcasse as localizações";
				mes "no seu mini-mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("^FF0000Corporação Rekenber^000000:Estação de Trem:Distrito Policial:Banco:Hotel:Aeroporto:Guilda dos Mercadores:Loja de Jóias:Loja de Armas:Loja de Departamento:Cancelar")) {
				case 1:
					mes "[Guia de Lighthalzen]";
					mes "Corporação Rekenber,";
					mes "a maior compania na";
					mes "República Schwartzwald,";
					mes "está localizada na parte Noroeste";
					mes "de Lighthalzen. Você não pode perder";
					mes "o prédio do Quartel General.";
					if (.@compass_check)
						viewpoint 1,102,247,2,0xFF0000;
					break;
				case 2:
					mes "[Guia de Lighthalzen]";
					mes "A Estação de Trem fica";
					mes "localizada no centro da";
					mes "da cidade,onde temos";
					mes "uma ferrovia direto à Einbroch.";
					if (.@compass_check)
						viewpoint 1,233,164,3,0xFF00FF;
					break;
				case 3:
					mes "[Guia de Lighthalzen]";
					mes "Nosso Distrito de Polícia fica";
					mes "ao norte do centro da cidade.";
					mes "Por favor, não hesite em reportar";
					mes "qualquer atividade ou pessoa";
					mes "suspeita, ou se tiver";
					mes "algum problema.";
					if (.@compass_check)
						viewpoint 1,236,276,4,0x99FFFFF;
					break;
				case 4:
					mes "[Guia de Lighthalzen]";
					mes "O Banco esta localizado";
					mes "exatamente oposto ao";
					mes "Distrito de Polícia de Lighthalzen,";
					mes "o que é uma ótima idéia";
					mes "quando eu penso nisso, na verdade.";
					if (.@compass_check)
						viewpoint 1,198,257,5,0x0000FF;
					break;
				case 5:
					mes "[Guia de Lighthalzen]";
					mes "Nosso Hotel está localizado";
					mes "no centro do Plaza Sul.";
					mes "Com seus serviços exelentes e";
					mes "luxuosas acomodações, esse";
					mes "hotel é extremamente popular.";
					if (.@compass_check)
						viewpoint 1,159,133,6,0x00FF00;
					break;
				case 6:
					mes "[Guia de Lighthalzen]";
					mes "O Aeroporto fica em nosso extremo";
					mes "oeste da Central de Passeios";
					mes "Você pode viajar para qualquer lugar na";
					mes "República Schwartzwald em um";
					mes "de nossos Aeroplanos.";
					if (.@compass_check)
						viewpoint 1,267,75,7,0x00FF00;
					break;
				case 7:
					mes "[Guia de Lighthalzen]";
					mes "A Guilda de Mercadores pode ser";
					mes "encontrado na parte sudoeste";
					mes "de Lighthalzen.";
					if (.@compass_check)
						viewpoint 1,74,53,8,0xFF99FF;
					break;
				case 8:
					mes "[Guia de Lighthalzen]";
					mes "A Loja de Jóias está";
					mes "localizada bem no oeste";
					mes "do Plaza Sul.";
					if (.@compass_check)
						viewpoint 1,93,110,9,0xFF9900;
					break;
				case 9:
					mes "[Guia de Lighthalzen]";
					mes "A Loja de Armas";
					mes "está localizada no fim da";
					mes "Central de Passeios.";
					mes "Vale a pena dar uma olhada";
					mes "se você quer ter uma";
					mes "aventura por aqui.";
					if (.@compass_check)
						viewpoint 1,196,46,10,0x330033;
					break;
				case 10:
					mes "[Guia de Lighthalzen]";
					mes "A Çoja de Departamento";
					mes "está localizada no meio de";
					mes "Lighthalzen e é o maior";
					mes "e mais conveniente lugar para";
					mes "comprar praticamente tudo.";
					if (.@compass_check)
						viewpoint 1,199,163,11,0xFFFF00;
					break;
				case 11:
					mes "[Guia de Lighthalzen]";
					mes "Por favor, me peça para 'Remover";
					mes "Indicações' se você";
					mes "não deseja mais ter as";
					mes "localizações marcadas";
					mes "no seu Mini-Mapa.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,102,247,2,0xFF0000;
			viewpoint 2,233,164,3,0xFF00FF;
			viewpoint 2,236,276,4,0x99FFFF;
			viewpoint 2,198,257,5,0x0000FF;
			viewpoint 2,159,133,6,0x00FF00;
			viewpoint 2,267,75,7,0x00FF00;
			viewpoint 2,74,53,8,0xFF99FF;
			viewpoint 2,93,110,9,0xFF9900;
			viewpoint 2,196,46,10,0x330033;
			viewpoint 2,199,163,11,0xFFFF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Lighthalzen]";
			mes "Avanços em magia e";
			mes "tecnologia nos permite";
			mes "modernizar nosso sistema de";
			mes "informação, permitindo que marquemos";
			mes "as localizações no seu Mini-Mapa";
			mes "para uma orientação mais fácil.";
			next;
			mes "[Guia de Lighthalzen]";
			mes "Seu Mini-Mapa esta localizado";
			mes "na parte superior direita";
			mes "da tela. Se você não consegue";
			mes "vê-la, pressione 'Ctrl + Tab'";
			mes "ou clique no botão 'Mapa'";
			mes "Na sua Janela de Informação Básica.";
			next;
			mes "[Guia de Lighthalzen]";
			mes "No seu Mini-Mapa,";
			mes "clique nos símbolos";
			mes "'+' e '-' para aproximar";
			mes "e afastar a visão no Mini-Mapa.";
			mes "Esperamos que aproveite sua viagem";
			mes "aqui em Lighthalzen.";
		case 4:
			mes "[Guia de Lighthalzen]";
			mes "Lighthalzen é divido em";
			mes "Distritos do Leste e Oeste.";
			mes "cortados por uma ferrovia";
			mes "que passa pelo meio.";
			mes "Temos sempre guardas para";
			mes "assegurar a paz no local.";
			next;
			mes "[Guia de Lighthalzen]";
			mes "Por favor, não hesite";
			mes "em reportar atividades";
			mes "ou pessoas suspeitas a nós.";
			mes "Esperamos que aproveite";
			mes "nossa cidade justa, aventureiro.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

lighthalzen,220,311,3	duplicate(LhzGuide)	Guia#2lhz	4_M_EIN_SOLDIER

lighthalzen,154,100,5	duplicate(LhzGuide)	Guia#3lhz	4_M_EIN_SOLDIER

lighthalzen,307,224,3	duplicate(LhzGuide)	Guia#4lhz	4_M_EIN_SOLDIER