//===== Cronus Script ========================================
//= Guias de Hugel
//===== Por: =================================================
//= erKURITA; L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.2c
//===== Compatível com: ======================================
//= Cronus SVN
//===== Changelog: ===========================================
//= 1.0 Started the script. [erKURITA]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.2a Traduzido. [RoM]
//= 1.2b Removido show	[Yum]
//= 1.2c Revisado	[KyukE]
//============================================================

hugel,98,56,3	script	Vovó Guia de Hugel#huge	4_F_LGTGRAND,0,0,{
	mes "[Vovó Guia de Hugel]";
	mes "Ah, olá~ você é um aventureiro cheio de energia.";
	mes "Bem-vindo a Hugel. Eu estou honrada de guiá-lo nesta bela cidade.";
	mes "Se for a primeira vez que você utiliza o serviço de um guia, seria bom dar uma olhada no menu ''Tutorial'' primeiro.";
	while(1) {
		next;
		switch(select("Indicações:Remover Indicações do Mini-Mapa:Tutorial:Cancelar")) {
		case 1:
			mes "[Vovó Guia de Hugel]";
			mes "Posso lhe indicar qualquer construção de Hugel.";
			mes "Então, onde você quer ir?";
			if (.@compass_check == 0) {
				mes "Quer que eu marque";
				mes "as localizações";
				mes "no seu Mini-Mapa?";
				next;
				if (select("Sim.:Não.") == 1) set .@compass_check,1;
			}
			set .@loop,1;
			while(.@loop) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("Igreja:Estalagem:Bar:Aeroporto:Loja de Armas:Loja de Utilidades:Loja de Artigos para Festas:^3131FFLocal de Mudança de Classe para Caçador^000000:^3131FFLocal da Expedição para o Templo^000000:Arena da Corrida de Monstros:Sala do Jogo de Bingo:Cancelar")) {
				case 1:
					mes "[Vovó Guia de Hugel]";
					mes "Bem, para mim, esta Igreja é mais um lugar para velhos tolos como eu, você sabe...";
					if (.@compass_check)
						viewpoint 1,156,116,2,0xFF0000;
					break;
				case 2:
					mes "[Vovó Guia de Hugel]";
					mes "É melhor você desfazer as malas antes de começar olhar por aí na cidade.";
					mes "É esta construção ao meu lado direito.";
					if (.@compass_check)
						viewpoint 1,104,79,3,0xFF00FF;
					break;
				case 3:
					mes "[Vovó Guia de Hugel]";
					mes "Sim, quando você viaja, você deseja ir a um bar para fazer novos amigos.";
					mes "Vá para o leste daqui, então você encontrará o bar.";
					if (.@compass_check)
						viewpoint 1,129,66,4,0x99FFFF;
					break;
				case 4:
					mes "[Vovó Guia de Hugel]";
					mes "Há pouco, estranhos vieram para a cidade e construíram este tipo aeroporto estranho...";
					mes "Como eles chamam isto? Aeroplano?";
					if (.@compass_check)
						viewpoint 1,178,146,5,0x0000FF;
					break;
				case 5:
					mes "[Vovó Guia de Hugel]";
					mes "Bem, nós temos uma loja de armas no centro da cidade.";
					mes "Mas eu não sei se você encontrará alguma arma útil lá.";
					if (.@compass_check)
						viewpoint 1,70,158,6,0x00FF00;
					break;
				case 6:
					mes "[Vovó Guia de Hugel]";
					mes "Sim, eu amo as Poções Vermelhas de Hugel. Eu nunca provei Poções Vermelhas de outros lugares ainda...hehehe. ";
					mes "A loja de utilidades é localizada no centro da cidade.";
					if (.@compass_check)
						viewpoint 1,93,167,7,0x00FF00;
					break;
				case 7:
					mes "[Vovó Guia de Hugel]";
					mes "A loja de artigos para festas fica perto do centro da cidade.";
					mes "Mas não use nenhum fogo de artifício perto de outras pessoas, porque é muito perigoso, você sabia?";
					if (.@compass_check)
						viewpoint 1,91,105,8,0xFF99FF;
					break;
				case 8:
					mes "[Vovó Guia de Hugel]";
					mes "Ah, você é um aspirante de Caçador?";
					mes "Então vá em direção ao nordeste seguindo a praia, então você encontrará o local de mudança de classe para Caçador.";
					if (.@compass_check)
						viewpoint 1,206,228,9,0xFF9900;
					break;
				case 9:
					mes "[Vovó Guia de Hugel]";
					mes "Eu ouvi dizer que a expedição para o templo está sendo organizada em uma casa ao oeste.";
					mes "Eles colocaram um tipo de placa no centro da cidade, então eu acho que eles estão recrutando pessoas para alguma coisa...";
					mes "Eu fico me perguntando o que eles estão fazendo lá...hmmm.";
					if (.@compass_check)
						viewpoint 1,52,91,10,0xFFFFFF;
					break;
				case 10:
					mes "[Vovó Guia de Hugel]";
					mes "Eu também gosto de jogar na Corrida de Monstros. É bem legal, não acha?";
					mes "Ah, você ainda não tentou? Então é melhor você ir lá checar. Confie em mim, você irá gostar.";
					if (.@compass_check)
						viewpoint 1,58,72,11,0xFF9900;
					break;
				case 11:
					mes "[Vovó Guia de Hugel]";
					mes "Você gosta de jogos de bingo? Se gostar, visite a Sala de Bingo Euklan.";
					if (.@compass_check)
						viewpoint 1,55,209,12,0x66FFFF;
					break;
				case 12:
					mes "[Vovó Guia de Hugel]";
					mes "Se você quiser que eu apague as indicações no seu mini-mapa,";
					mes "me peça, escolhendo 'Remover Indicações' no menu.";
					set .@loop,0;
				}
			}
			break;
		case 2:
			viewpoint 2,156,116,2,0xFF0000;
			viewpoint 2,104,79,3,0xFF00FF;
			viewpoint 2,129,66,4,0x99FFFF;
			viewpoint 2,178,146,5,0x0000FF;
			viewpoint 2,70,158,6,0x00FF00;
			viewpoint 2,93,167,7,0x00FF00;
			viewpoint 2,91,105,8,0xFF99FF;
			viewpoint 2,206,228,9,0xFF9900;
			viewpoint 2,52,91,10,0xFFFFFF;
			viewpoint 2,58,72,11,0xFF9900;
			viewpoint 2,55,209,12,0x66FFFF;
			set .@compass_check,0;
			mes "[Vovó Guia de Hugel]";
			mes "Ok, eu apaguei tudo. Se você quiser saber mais sobre algum local daqui, venha me procurar.";
			break;
		case 3:
			mes "[Vovó Guia de Hugel]";
			mes "Quando você está usando o Guia de 'Indicações',";
			mes "tenha certeza de que eles estão aparecendo no canto superior direito de sua tela.";
			mes "Se você não está vendo seu mini-mapa, aperte 'Ctrl + Tab' ou clique no botão 'Mapa' na sua Janela de Informações Básicas, ok?";
			mes "Você ainda pode diminuir o zoom no mini-mapa apertando o botão '-' no caso de você não estar vendo o mapa da vila por inteiro.";
			break;
		case 4:
			mes "[Vovó Guia de Hugel]";
			mes "Este trabalho de Guia é tão excitante. Hohoho~";
			close;
		}
	}
}