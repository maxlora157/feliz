//===== Cronus Script ======================================
//= Refinador Avançado
//===== Por: ==================================================
//= L0ne_W0lf
//===== Versão Atual: =====================================
//= 1.5a
//===== Descrição: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= After a conversation with Doddler, it's been established that
//= the advanced refiner works similar the the "Bubble Gum" item.
//= The success percentage is not "increased" however, if it fails
//= You get a second try. This tries twice at the same time,
//= effectively giving you a re-roll on your attempt.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Comentários Adicionais: =================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//= 1.5 Some official script updates. [Euphy]
//= 1.5a Traduzido [BOO]
//============================================================

payon,157,146,6	script	Suhnbi#cash	4_M_03,{
	disable_items;
	mes "[Suhnbi]";
	mes "Eu sou o Refinador";
	mes "Eu posso refinar todos os tipos de armas,";
	mes "armaduras e equipamentos, então deixe-me";
	mes "sabe o que quer para refinar.";
	next;
	setarray .@position$[1], "Cabeça","Corpo","Mão Esquerda","Mão Direita","Manto","Sapatos","Acessorio 1","Acessorio 2","Cabeça 2","Cabeça 3";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@i)) {
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[Suhnbi]";
		mes "Eu não acho que eu possa aperfeiçoar todos estes itens.";
		close;
	}
	set .@part, select(.@menu$);

	if (!getequipisequiped(.@part)) //custom check
		close;
	if (!getequipisenableref(.@part)) {
		mes "[Suhnbi]";
		mes "Vá encontrar outro Ferreiro. Eu não posso aperfeiçoar isto.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Suhnbi]";
		mes "Hmm ... alguém já aperfeiçoou isto. Eu não posso aperfeiçoá-lo mais.";
		close;
	}

	// Make sure you have the neccessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	switch(getequipweaponlv(.@part)) {
		case 1: callsub S_RefineValidate,1,7620,50,.@part; break;
		case 2: callsub S_RefineValidate,2,7620,200,.@part; break;
		case 3: callsub S_RefineValidate,3,7620,5000,.@part; break;
		case 4: callsub S_RefineValidate,4,7620,20000,.@part; break;
		default: callsub S_RefineValidate,0,7619,2000,.@part; break;
	}

	mes "[Suhnbi]";
	mes "Clang! Clang! Clang!";
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		successrefitem .@part;
		next;
		emotion e_no1;
		mes "[Suhnbi]";
		mes "There you go! It's done.";
		mes "Faz muito tempo desde que eu fiz uma "+((getequipweaponlv(.@part))?"Arma":"Armadura")+" tão forte. Bem você deve estar feliz, porque se tornou mais forte!";
		close;
	}
	failedrefitem .@part;
	next;
	emotion (!rand(5))?e_cash:e_omg;
	mes "[Suhnbi]";
	mes "Uuuuuuuuuummmmmph!!!";
	next;
	mes "[Suhnbi]";
	mes "...";
	mes ".....";
	mes ".......Huhuhuhuhu~";
	mes "........	Foi sua escolha e minha capacidade, não me arrependo.";
	close;

S_RefineValidate:
	mes "[Suhnbi]";
	if (getarg(0))
		mes "Você deseja Refinar 1 Nível da arma " + getarg(0) + "?";
	mes "Para refinar isso eu preciso de um ^ff9999" + getitemname(getarg(1)) + "^000000 e " + getarg(2) + " zeny.";
	mes "Você deseja continuar?";
	next;
	if(select("Sim:Não") == 1) {
		if (getequippercentrefinery(getarg(3)) < 100) {
			if (getarg(0)) {
				mes "[Suhnbi]";
				mes "Wow!!";
				mes "Esta arma, provavelmente";
				mes "parece que foi refinada...";
				mes "muitas vezes	...";
				mes "ele pode quebra se";
				mes "for refinado novamente.";
				next;
				mes "E se ela quebrar,";
				mes "você não poderá usá-la mais!";
				mes "Todas a cartas e propriedades do equipamento";
				mes "^ff0000serão perdidas^000000!!";
				mes "^ff0000 além disso, o equipamento vai quebrar!^000000";
				mes "Tem certeza que ainda quer continuar?";
				next;
				if(select("Sim:Não") == 2) {
					mes "[Suhnbi]";
					mes "Bem..";
					mes "Porque, se a arma quebrar da refinação razoável, ficarei de mau humor, também.";
					close;
				}
			} else {
				mes "[Suhnbi]";
				mes "Giggle. Giggle. Oh, você tem coragem e ousadia para refina-lo.";
				mes "Você sabe que é muito arriscado, não é?";
				next;
				mes "Se o seu equipamento defensivo é quebrado, você nunca vai ser capaz de usá-lo novamente.";
				mes "Até mesmo suas cartas e modificações ^ff0000desaparecerão completamente^000000.";
				//mes "Everything will disappear. As in... GONE!";
				mes "Você realmente deseja continuar?";
				next;
				if(select("Sim:Não") == 2) {
					mes "[Suhnbi]";
					mes "Que absurdo. Você perder meu precioso tempo.";
					mes "Cai fora, punk.";
					close;
				}
			}
		}
		if (countitem(getarg(1)) > 0 && Zeny > getarg(2)) {
			delitem getarg(1),1;
			set Zeny, Zeny - getarg(2);
			return;
		}
		mes "[Suhnbi]";
		mes "Are these all you have?";
		mes "Sinto muito, mas eu não posso fazer nada sem todos os materiais. Além disso, eu mereço alguns pagamentos pelo meu trabalho, não é?";
		close;
	}
	mes "[Suhnbi]";
	mes "Não posso ajudá-lo, mesmo se você não estiver feliz com isso ...";
	close;
}
