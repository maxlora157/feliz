//===== Cronus Script ======================================
//= Headgear Dyer (Cash)
//===== Por: ==================================================
//= Xantara
//= Maud_Dib
//===== Versão Atual: =====================================
//= 1.0
//===== Descrição: =========================================
//= [Official Conversion]
//= Change headgear's color. Cost is a Mysterious Dyestuff
//===== Comentários Adicionais: =================================
//= 1.0 First / Optimized Version
//============================================================

prt_in,253,168,4	script	Alora	4_F_LGTGIRL,{

// Mysterious Dyestuff
	set .@dyeID, 6220;
// Headgear Choices
	setarray .@hgn$[1], "Mage Hat","Beanie","Drooping Cat","Deviruchi Hat","Wig","Ribbon","Magestic Goat";
// Mage Hats - Normal, Yellow, Blue, Brown, Gray, Red
	setarray .@hg_1[1], 5027, 5242, 5241, 5240, 5239, 5238;
// Beanies - Normal, Pink, Blue, Brown
	setarray .@hg_2[1], 5076, 5237, 5236, 5235;
// Drooping Cats - Normal, Blue, Brown, Gray, Pink, Yellow
	setarray .@hg_3[1], 5058, 5233, 5231, 5230, 5232, 5234;
// Deviruchi Hats - Normal, Red, Gray, Brown
	setarray .@hg_4[1], 5038, 5227, 5228, 5229;
// Wigs - Happy, Shiny, Marvelous, Fantastic
	setarray .@hg_5[1], 5273, 5274, 5275, 5276;
// Ribbons - Normal, Black, Yellow, Green, Pink, Red, Orange, White
	setarray .@hg_6[1], 2208, 5191, 5192, 5193, 5194, 5195, 5196, 5197;
// Magestic Goats - Normal, Evolved
	setarray .@hg_7[1], 2256, 5217;

	mes "[Alora]";
	mes "Olá, eu posso mudar a cor do seu capacete se você me trazer um Mysterious Dyestuff.";
	next;
	mes "[Alora]";
	mes "Você tem um capacete que você gostaria de tingir?";
	next;
	for(set .@i,1; .@i < getarraysize(.@hgn$); set .@i,.@i+1) {
		if(.@i == 1)    set .@menu$, .@hgn$[.@i];
		else            set .@menu$, .@menu$ +":"+ .@hgn$[.@i];
	}
	set .@menu$, .@menu$ +":Cancel";
	set .@t, select(.@menu$);
	if(.@t == getarraysize(.@hgn$)) {
		mes "[Alora]";
		mes "Have a good journey adventurer!";
		mes "Se você já está curioso para experimentar uma nova cor em sua chapelaria Kafra ou aqueles que você encontrou em suas aventuras por favor, venha para mim!";
		close;
	}
	mes "[Alora]";
	mes "Ok, qual  cor você quer mudá-lo para?";
	next;
	for(set .@i,1; .@i < getarraysize(getd(".@hg_"+.@t+"")); set .@i,.@i+1) {
		if(.@i == 1 && .@t != 5)
			set .@menu$, "Normal";
		else if(.@i == 1 && .@t == 5)
			set .@menu$, getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
		else {
			sscanf(getitemname(getd(".@hg_"+.@t+"["+.@i+"]")),"%s "+ .@hgn$[.@t],.@colour$);
			set .@menu$, .@menu$ +":"+ .@colour$;
		}
	}
	set .@menu$, .@menu$ +":Cancel";
	set .@c, select(.@menu$);
	if(.@c == getarraysize(getd(".@hg_"+.@t+""))) {
		mes "[Alora]";
		mes "Oh, okay no problem!";
		close;
	}
	set .@dyHG, getd(".@hg_"+.@t+"["+.@c+"]");
	mes "[Alora]";
	mes "Oh, eu estou tão animado que você não está?";
	mes "E só para ter certeza, de que cor é o "+ .@hgn$[.@t] +" você quer que eu use?";
	next;
	mes "[Alora]";
	mes "Por favor, entenda que eu vou usar o^FF00001st "+ .@hgn$[.@t] +" De que a cor em seu inventário^000000! ";
	next;
	mes "[Alora]";
	mes "Quaisquer atualizações e cartões será dissolvido pelo corante misterioso, tenha certeza que você está^0000FF+0 "+ getitemname(.@dyHG) +" sem cartões.^000000";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Ok, obrigado pelo aviso, eu acho que vou dar o meu";
	next;
	set .@menu$, "Nevermind";
	for(set .@i,1; .@i < getarraysize(getd(".@hg_"+.@t+"")); set .@i,.@i+1) {
		if(.@i == 1)    set .@menu$, .@menu$ +":Normal "+ getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
		else            set .@menu$, .@menu$ +":"+ getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
	}
	set .@mine, select(.@menu$) - 1;
	if(!.@mine) {
		mes "[Alora]";
		mes "É melhor estar muito certo, e ter uma viagem segura.";
		close;
	}
	set .@myHG, getd(".@hg_"+.@t+"["+.@mine+"]");
	if(.@dyHG == .@myHG) {
		mes "[Alora]";
		mes "O que aconteceu?";
		close;
	}
	if(!countitem(.@dyeID) || !countitem(.@myHG)) {
		mes "[Alora]";
		mes "Oh meu Deus, você parece estar faltando alguma coisa.";
		close;
	}
	mes "[Alora]";
	mes "Parece ótimo, não é?"; 
	mes "Espero que você vai voltar a tingir capacetes mais bonitos!";
	delitem .@dyeID,1; // Dyestuff
	delitem .@myHG,1;  // Your headgear
	getitem .@dyHG,1;  // Dyed headgear
	close;
}
